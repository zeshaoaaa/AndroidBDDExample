apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'

// BDD 配置
apply from: '../cucumber.gradle'

// 测试覆盖率
apply from: '../jacoco.gradle'

// Spec 配置
apply from: '../spek.gradle'


android {
    compileSdkVersion 29
    buildToolsVersion "29.0.0"
    defaultConfig {
        applicationId "org.jay.example"
        minSdkVersion 15
        targetSdkVersion 29
        versionCode 1
        versionName "1.0"
        // 这个项目只有 Cucumber 测试，不用动态替换
        testInstrumentationRunner 'org.jay.example.runner.CucumberTestRunner'
        // testInstrumentationRunner getInstrumentation(project)
    }
    buildTypes {

        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }


}

/**
 * 根据运行任务时的参数，动态获取 Instrumentation 测试运行器
 */
def getInstrumentation(Project project) {
    project.hasProperty('cucumber') ? 'org.jay.example.runner.CucumberTestRunner' :
            'androidx.test.runner.AndroidJUnitRunner'
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation 'androidx.appcompat:appcompat:1.0.2'
    implementation 'androidx.core:core-ktx:1.0.2'
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'

    // 函数式编程
    implementation 'io.reactivex.rxjava2:rxandroid:2.1.1'

    // 依赖注入框架
    implementation "org.koin:koin-android:$koin_version"

    // ------------------------- 测试框架 start-------------------------

    testImplementation 'junit:junit:4.12'

    // MockK
    testImplementation "io.mockk:mockk:$mockk_version"

    // MockK
    androidTestImplementation("io.mockk:mockk-android:$mockk_version")
            { exclude module: 'objenesis' }
    androidTestImplementation 'org.objenesis:objenesis:2.6'

    androidTestImplementation 'androidx.test:rules:1.1.0'
    androidTestImplementation 'androidx.test:runner:1.1.1'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.1.1'

    // Koin 测试
    androidTestImplementation "org.koin:koin-test:$koin_version"

    // Android BDD 框架
    androidTestImplementation "io.cucumber:cucumber-android:$cucumber_version"
    androidTestImplementation "io.cucumber:cucumber-picocontainer:$cucumber_version"



    // ------------------------- 测试框架 end-------------------------

}

